&ACCESS RVP
&REL 3
&PARAM SensorITMASK = *
&PARAM TEMPLATE = c:\KRC\Roboter\Template\vorgabe
&PARAM DISKPATH = KRC:\R1\Program
def ekiMain( )
;Copyright (C) 2020 KUKA Robotics Corporation
;fold Disclaimer
; This sample code is provided as is and any express or implied warranties, 
; including the implied warranties of merchantability and fitness
; for a particular purpose are disclaimed. In no event shall KUKA or contributors
; be liable for any direct, indirect, incidental, special, exemplary, or consequential
; damages (including, but not limited to, procurement of substitute goods or services;
; loss of use, data, or profits; or business interruption) sustained by you or a third
; party, however caused and on any theory of liability, whether in contract, strict 
; liability, or tort arising in any way out of the use of this sample code, even if 
; advised of the possibility of such damage.
;endfold
  int i
  decl eki_status ret
  e6pos testPos
  e6axis tempAxis
  frame tempFrame
  
  bas (#INITMOV, 0 )
 ptp $axis_act
ptp xhome

  loop
    wait for commandAvailable 
    
    ; execute the command
    switch cmdData.ekiCmd
       ;fold MOTION COMMANDS
       case #PTP_TO_JOINT
         ptp cmdData.jointVal
         wait sec 0
         ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiSuccess[])
         
       case #PTP_TO_CART
         ptp cmdData.cartVal
         wait sec 0
         ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiSuccess[])
         
       case #PTP_TO_FRAME
         tempFrame = cmdData.cartVal
         ptp tempFrame
         wait sec 0
         ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiSuccess[])
           ;endfold
           
        ;fold SET COMMANDS     
        case #SET_TOOL_DATA
          $tool = cmdData.cartVal
          wait sec 0
          ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiSuccess[])
          
        case #SET_BASE_DATA
          $base = cmdData.cartVal
          wait sec 0
          ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiSuccess[])
        
        case #SET_LOAD_DATA
          $load.m = cmdData.RealVal
          $load.cm.x = cmdData.cartVal.x
          $load.cm.y = cmdData.cartVal.y
          $load.cm.z = cmdData.cartVal.z
          $load.cm.a = cmdData.cartVal.a
          $load.cm.b = cmdData.cartVal.b
          $load.cm.c = cmdData.cartVal.c
          $load.j.x = cmdData.cartVal.e1
          $load.j.y = cmdData.cartVal.e2
          $load.j.z = cmdData.cartVal.e3           
          ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiSuccess[])

        case #SET_JOINT_SPEED
          if ((cmdData.realVal < 0.0) or (cmdData.realVal > 100.0)) then
            ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiInvalidValue[])
          else 
            for i = 1 to 6
              $VEL_AXIS[i] = cmdData.realVal
            endfor
            $VEL_EXTAX[1] = cmdData.realVal
            ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiSuccess[])
          endif
          
        case #SET_CART_SPEED
          if ((cmdData.realVal < 0.0) or (cmdData.realVal > $VEL_MA.CP)) then
            ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiInvalidValue[])
          else 
            $VEL.CP = cmdData.realVal
            ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiSuccess[])
          endif
          
        case #SET_JOINT_ACCEL
          if ((cmdData.realVal < 0.0) or (cmdData.realVal > 100.0)) then
            ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiInvalidValue[])
          else 
            for i = 1 to 6
              $ACC_AXIS[i] = cmdData.realVal
            endfor
            $ACC_EXTAX[1] = cmdData.realVal
            ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiSuccess[])
          endif
          
        case #SET_CART_ACCEL
          if ((cmdData.realVal < 0.0) or (cmdData.realVal > $ACC_MA.CP)) then
            ret = SendString(ekiConfigFile[], cmdData.cmdId, ekiInvalidValue[], cmdData.cmdName[])
          else 
            $ACC.CP = cmdData.realVal
            ret = SendString(ekiConfigFile[], cmdData.cmdId, ekiSuccess[], cmdData.cmdName[])
          endif
        ;endfold
        
        ;fold GET COMMANDS
        case #GET_RUNMODE
          ret = SendProgRunMode(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[])
        ;endfold
      default
        ret = SendString(ekiConfigFile[], cmdData.cmdId, cmdData.cmdName[], ekiInvalidCmd[])
    endswitch
    wait sec 0
    commandAvailable = false ;
  endloop
end
